&НаСервере
Процедура ОбновитьДиалоги()
	Диалоги.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	GigaChat_Диалоги.ИдентификаторДиалога КАК ИдентификаторДиалога,
	               |	GigaChat_Диалоги.Наименование КАК Наименование
	               |ИЗ
	               |	РегистрСведений.GigaChat_Диалоги КАК GigaChat_Диалоги";
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаЗапроса = РезультатЗапроса.Выбрать();
	Пока ВыборкаЗапроса.Следующий() Цикл
		НС = Диалоги.Добавить();
		НС.ИдентификаторДиалога = ВыборкаЗапроса.ИдентификаторДиалога;
		НС.Наименование = ВыборкаЗапроса.Наименование;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДиалог(Команда)
	Наименование = "";
	Если ВвестиСтроку(Наименование,"Введите название(Topic) диалога",0,Ложь) Тогда
		НовыйДиалог = GigaChat_Сервер.СоздатьДиалог(Наименование);
		НС = Диалоги.Добавить();
		НС.ИдентификаторДиалога = НовыйДиалог.ИдентификаторДиалога;
		НС.Наименование = НовыйДиалог.Наименование;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьДиалоги();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьДиалог(Команда)
	ТД = ЭтаФорма.Элементы.Диалоги.ТекущиеДанные;
	Если ТД <> Неопределено Тогда
		GigaChat_Сервер.УдалитьДиалог(Новый УникальныйИдентификатор(ТД.ИдентификаторДиалога));
		ОбновитьДиалоги();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДиалогиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторДиалога",Элемент.ТекущиеДанные.ИдентификаторДиалога);
	ПараметрыФормы.Вставить("Наименование",Элемент.ТекущиеДанные.Наименование);
	ОткрытьФорму("ОбщаяФорма.ФормаСообщения",ПараметрыФормы,ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

